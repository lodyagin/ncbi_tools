$!
$!  MAKENET.TGV (OpenVMS AXP and OpenVMS VAX, combined)
$!
$!   Usage: @makenet.tgv
$!
$!   Builds the network interface, Entrez network access, and 
$!    client TCP libraries for TGV, Inc. Multinet.
$!
$!   Creates the applications:
$!
$!    NetEntCf - A version of the Entrez configuration program, EntrezCf,
$!                  which includes network support
$!    NEntrez  - Network version of Entrez
$!    CEntrez  - Combo version of Entrez which works with CD-ROMs
$!                  and Network services.  This version uses CD-ROM data
$!                  when available, network services otherwise.
$!
$!   William A. Gilbert
$!   Dept of Biochemistry and Molecular Biology
$!   College of Life Sciences and Agriculture
$!   University of New Hampshire
$!   Durham, NH 03824
$!
$!   Will.Gilbert@unh.edu
$!
$!--------------------------------------------------------------------------
$!
$! Compiler settings.  This procedure requires DEC C v4.0 or greater.
$!
$ CC = "cc/decc/nowarnings"
$ define/nolog DECC$SYSTEM_INCLUDE [-.include],-
                                   multinet_root:[multinet.include.sys],-
                                   DECC$LIBRARY_INCLUDE
$!--------------------------------------------------------------------------
$! Move Network include files into NCBI include directory
$!
$ copy [-.network.entrez.client]*.h  [-.include]      ! Entrez network access
$ copy [-.network.nsclilib]*.h [-.include]            ! Network services 
$ purge [-.include]
$!--------------------------------------------------------------------------
$!
$! Set up network include directory definitions. These are specific for
$! the TGV, Inc. Multinet TCP/IP suite.
$!
$ define/NoLog sys     multinet_root:[multinet.include.sys]
$ define/NoLog netinet multinet_root:[multinet.include.netinet]
$ define/NoLog arpa    multinet_root:[multinet.include.arpa]
$!
$!--------------------------------------------------------------------------
$!
$! Make Network Interface library, netcli
$!
$ CC/define=NETP_INET_TGV [-.network.nsclilib]ni_lib.c
$ CC/define=NETP_INET_TGV [-.network.nsclilib]ni_msg.c
$ CC [-.network.NSCLILIB]ni_error.c
$ CC [-.network.NSCLILIB]ni_encrs.c
$!
$ librarian/object/create [-.lib]netcli.olb  -
          ni_lib.obj, ni_msg.obj, ni_error.obj, ni_encrs.obj
$ delete ni_lib.obj;*, -
         ni_msg.obj;*, -
         ni_error.obj;*, -
         ni_encrs.obj;*
$!
$!--------------------------------------------------------------------------
$!
$! Make Network Dispatcher/Entrez access library, netentr
$!
$ CC [-.network.entrez.client]netentr.c
$ CC [-.network.entrez.client]netlib.c
$ CC [-.network.entrez.client]objneten.c
$!
$ librarian/create/object [-.lib]netentr.olb  -
         netentr.obj, netlib.obj, objneten.obj
$!
$ delete netentr.obj;*, -
         netlib.obj;*, -
         objneten.obj;*
$!
$!--------------------------------------------------------------------------
$!
$! Make library which depends upon both Vibrant and the network client
$!
$ CC [-.network.vibnet]netcnfg
$ CC [-.network.vibnet]trmlst
$ CC [-.network.vibnet]docsum
$!
$ librarian/create/object [-.lib]vibnet.olb  netcnfg.obj, trmlst.obj, docsum.obj
$ delete netcnfg.obj;*,trmlst.obj;*,docsum.obj;*
$!
$!=====================  A P P L I C A T I O N S  ==========================
$!
$! Make version of EntrezCf which has network support
$!
$ CC/define=(NETP_INET_TGV,WIN_MOTIF)/object=netentcf -
          [-.demo]netentcf
$!
$!  Link NetEntCf, the network version
$!
$ link/exe=netentcf netentcf.obj, sys$input/option
[-.lib]libvibrant.olb/library
[-.lib]libncbicdr.olb/library
[-.lib]vibnet.olb/library
[-.lib]netentr.olb/library
[-.lib]netcli.olb/library
[-.lib]libncbi.olb/library
multinet:multinet_socket_library.exe/shareable
sys$library:vaxcrtl.olb/library
sys$share:decw$mrmlibshr12.exe/share
sys$share:decw$dxmlibshr12.exe/share
sys$share:decw$xmlibshr12.exe/share
sys$share:decw$xtlibshrr5.exe/share
sys$share:decw$xlibshr.exe/share
sys$share:decc$shr.exe/shareable
$!----------------------------------------------------------------------------
$! Remove the DEC C system include symbol
$!
$ deassign DECC$SYSTEM_INCLUDE
$!
$ exit

