(* ================================================================================
	makeall.met

	An AppleScript to compile the NCBI Toolkit base libraries with
	Metrowerks CodeWarrior.

	Revision History:
	----------------

	??/??/??	Kans		Created
	10/11/95	Corarito	Modified for CW7
	11/22/95	Harmon		CopyHdrs script must be run first
	 2/18/96	Kans		Consolidated all 68K/PPC scripts
	 3/15/00	Churchill   Modified for OpenTransport work in progress
	 4/19/00	Kans		Only need to copy ncbilcl.met to ncbilcl.h

	This file now subsumes the functions of the old copyhdrs.met AppleScript.

	In order to compile for 68K, change the MASTER property.

	================================================================================ *)

(* ==== Uncomment one of the MASTER symbols ==== *)

property MASTER : "PPC"
-- property MASTER : "68K"

property MAKELIBS : "TRUE"

(* ==== These symbols will be derived ==== *)

global PREFIX
global SUFFIX

global HARDDISK
global METRO
global METROWERKSCW
global OTLIB

global NCBISOURCE
global LIBRARIES
global BUILDTEST

global INCLUDEFILES
global DOOPENTPT
global OPTIMIZE

(* ==== These subroutines set paths and other preferences during population ==== *)

on DoSetGlobals()
	if MASTER = "PPC" then
		set PREFIX to "PPC "
		set SUFFIX to "ppc"
	else if MASTER = "68K" then
		set PREFIX to "68K "
		set SUFFIX to "68k"
	end if
	
	tell application "Finder"
		
		set HARDDISK to startup disk as string
		set METRO to HARDDISK & "CodeWarrior Pro 5:" as string
		set METROWERKSCW to METRO & "Metrowerks CodeWarrior:" as string
		set OTLIB to METROWERKSCW & "MacOS Support:OpenTransport:Open Tpt Client Developer:PPC Libraries:"
		
		set NCBISOURCE to METRO & "ncbi:" as string
		
		set INCLUDEFILES to NCBISOURCE & "include:" as string
		set LIBRARIES to NCBISOURCE & "lib:" as string
		set BUILDTEST to NCBISOURCE & "build:" as string
		
		if MASTER = "PPC" and (exists (file "debug.flg" of folder METRO)) then
			set OPTIMIZE to "FALSE"
		else
			set OPTIMIZE to "TRUE"
		end if
		
		if MASTER = "PPC" and (exists (folder OTLIB)) then
			set DOOPENTPT to "TRUE"
		else if MASTER = "PPC" then
			set DOOPENTPT to "FALSE"
			display dialog "Did not find OpenTransport folder inside MacOS Support folder" & return & Â
				return & "Will use MacTCP interfaces to build networking" & return Â
				buttons "OK" default button 1
		else
			set DOOPENTPT to "FALSE"
		end if
		
	end tell
end DoSetGlobals

on CreateFolders()
	tell application "Finder"
		
		if not (exists (folder INCLUDEFILES)) then
			make new folder at folder NCBISOURCE with properties {name:"include"}
		end if
		
		if not (exists (folder LIBRARIES)) then
			make new folder at folder NCBISOURCE with properties {name:"lib"}
		end if
		
		if not (exists (folder BUILDTEST)) then
			make new folder at folder NCBISOURCE with properties {name:"build"}
		end if
		
	end tell
end CreateFolders

on CopyPrefixes()
	tell application "Finder"
		
		if (exists (file "ncbilcl.met" of folder INCLUDEFILES)) then
			delete file "ncbilcl.met" of (folder INCLUDEFILES)
		end if
		
		if (exists (file "ncbilcl.h" of (folder INCLUDEFILES))) then
			delete file "ncbilcl.h" of (folder INCLUDEFILES)
		end if
		
		copy {file "ncbilcl.met" of (folder "corelib" of folder NCBISOURCE)} to {folder INCLUDEFILES}
		set name of file "ncbilcl.met" of (folder INCLUDEFILES) to "ncbilcl.h"
		
	end tell
end CopyPrefixes

on DoSetPreferences()
	tell application "CodeWarrior IDE 4.0.4"
		
		(* ===== Panel Target ===== *)
		if MASTER = "PPC" then
			Set Preferences of panel "Target Settings" to {Linker:"MacOS PPC Linker"}
		else if MASTER = "68K" then
			Set Preferences of panel "Target Settings" to {Linker:"MacOS 68K Linker"}
		end if
		
		(* ===== Panel Project ===== *)
		if MASTER = "PPC" then
			Set Preferences of panel "PPC Project" to {Project Type:library}
		else if MASTER = "68K" then
			Set Preferences of panel "68K Project" to {Project Type:library}
		end if
		
		(* ===== Panel C/C++ Language =====  *)
		Set Preferences of panel "C/C++ Compiler" to Â
			{Activate CPlusPlus:false Â
				, ARM Conformance:false Â
				, Exception Handling:true Â
				, Inlining:inline_none Â
				, RTTI:false Â
				, Pool Strings:false Â
				, Dont Reuse Strings:false Â
				, Require Function Prototypes:true Â
				, ANSI Strict:false Â
				, ANSI Keywords Only:false Â
				, Expand Trigraph Sequences:false Â
				, MPW Newlines:false Â
				, MPW Pointer Type Rules:false Â
				, Enums Always Ints:false Â
				, Prefix File:""}
		
		(* ===== Panel C/C++ Warnings ===== *)
		Set Preferences of panel "C/C++ Warnings" to Â
			{Treat Warnings As Errors:false Â
				, Illegal Pragmas:true Â
				, Empty Declarations:true Â
				, Possible Errors:true Â
				, Unused Variables:true Â
				, Unused Arguments:false Â
				, Extra Commas:true Â
				, Extended Error Checking:true Â
				, Hidden Virtual Functions:true Â
				, Implicit Arithmetic Conversions:false Â
				, NonInlined Functions:true Â
				, Inconsistent Class Struct:true}
		
		if MASTER = "PPC" then
			
			(* ===== Panel PPC Processor =====  *)
			Set Preferences of panel "PPC CodeGen" to Â
				{Struct Alignment:Align_PPC Â
					, Make Strings ReadOnly:true Â
					, Store Data in TOC:true Â
					, Use FMADD Instructions:false Â
					, Use Profiler:false Â
					, Traceback Tables:TB_None Â
					, Schedule:false Â
					, Peephole Optimizer:false}
			
			(* ===== Panel PPC Global Optimizer =====  *)
			if OPTIMIZE = "TRUE" then
				Set Preferences of panel "PPC Global Optimizer" to Â
					{Optimize For:code_Speed Â
						, Level:1}
			else
				Set Preferences of panel "PPC Global Optimizer" to Â
					{Optimize For:code_Speed Â
						, Level:0}
			end if
			
			(* ===== Panel PPC Linker ===== *)
			Set Preferences of panel "PPC Linker" to Â
				{Generate SYM File:true Â
					, Full Path In Sym Files:true Â
					, Generate Link Map:false Â
					, Suppress Warnings:false Â
					, Link Mode:fast Â
					, Initialization Name:Â
					"", Main Name:Â
					"__start", Termination Name:""}
			
			(* ===== Panel PPC PEF =====  *)
			Set Preferences of panel "PPC PEF" to Â
				{Export Symbols:none Â
					, Old Definition:0 Â
					, Old Implementation:0 Â
					, Current Version:0 Â
					, Code Sorting:nosort Â
					, Share Data Section:false Â
					, Expand Uninitialized Data:false Â
					, Fragment Name:""}
			
			(* ===== Panel Debugger =====  *)
			Set Preferences of panel "Debugger Target" to Â
				{Log System Messages:false Â
					, Stop At Watchpoints:true}
			
		else if MASTER = "68K" then
			(* ===== Panel 68K Processor =====  *)
			Set Preferences of panel "68K CodeGen" to Â
				{Code Model:large Â
					, Struct Alignment:Align_68k Â
					, MC68020 CodeGen:false Â
					, Floating Point CodeGen:SANE Â
					, PC Relative Strings:false Â
					, Use Profiler:false Â
					, Four Bytes Ints:true Â
					, Eight Byte Double:true Â
					, Far Data:true Â
					, Far Method Tables:true Â
					, Far String Constants:true Â
					, MPW Calling Conventions:false}
			
			(* ===== Panel 68K Linker ===== *)
			Set Preferences of panel "68K Linker" to Â
				{Generate SYM File:true Â
					, Full Path In Sym Files:true Â
					, Generate Link Map:false Â
					, Link Single Segment:false Â
					, Suppress Warnings:false Â
					, Fast Link:true Â
					, Merge Compiler Glue:false}
			
		end if
		
		my DoSetPaths1()
		
	end tell
end DoSetPreferences

on DoSetPaths1()
	tell application "CodeWarrior IDE 4.0.4"
		
		(* ===== Panel Access Paths ===== *)
		-- We need to be able to move the default system compiler folder to the end of the list
		-- because certain files like "all.h" and "all" are defined by both Metrowerks and NCBI.
		-- We want to find the NCBI "all.h" first.
		Set Preferences of panel "Access Paths" to {System Paths:{}} -- This removes the compiler default folder
		
		Set Preferences of panel "Access Paths" to {Convert Paths:true}
		Set Preferences of panel "Access Paths" to {User Paths:{{name:NCBISOURCE, recursive:true, origin:absolute}}}
		Set Preferences of panel "Access Paths" to {System Paths:{{name:NCBISOURCE, recursive:true, origin:absolute}}} -- Added for speed
		
	end tell
end DoSetPaths1

on DoSetPaths2()
	tell application "CodeWarrior IDE 4.0.4"
		
		-- After we have added all paths, reinsert compiler default at end of list
		Set Preferences of panel "Access Paths" to {System Paths:{{name:":MacOS Support:Headers:", recursive:true, origin:shell relative}}}
		Set Preferences of panel "Access Paths" to {System Paths:{{name:":MacOS Support:Libraries:", recursive:true, origin:shell relative}}}
		Set Preferences of panel "Access Paths" to {System Paths:{{name:":MacOS Support:Universal:", recursive:true, origin:shell relative}}}
		Set Preferences of panel "Access Paths" to {System Paths:{{name:":MacOS Support:Profiler:", recursive:true, origin:shell relative}}}
		Set Preferences of panel "Access Paths" to {System Paths:{{name:":MSL:MSL_C:", recursive:true, origin:shell relative}}}
		if DOOPENTPT = "TRUE" then
			Set Preferences of panel "Access Paths" to {System Paths:{{name:":MacOS Support:OpenTransport:", recursive:true, origin:shell relative}}}
		end if
		
	end tell
end DoSetPaths2

on DoCompileLibraries()
	tell application "CodeWarrior IDE 4.0.4"
		
		open (LIBRARIES & "ncsasock" & SUFFIX & ".µ")
		Make Project
		Close Project
		
		if DOOPENTPT = "TRUE" then
			open (LIBRARIES & "ncsasockOT" & SUFFIX & ".µ")
			Make Project
			Close Project
		end if
		
		open (LIBRARIES & "ncbi" & SUFFIX & ".µ")
		Make Project
		Close Project
		
		open (LIBRARIES & "ncbiobj" & SUFFIX & ".µ")
		Make Project
		Close Project
		
		open (LIBRARIES & "ncbicdr" & SUFFIX & ".µ")
		Make Project
		Close Project
		
		open (LIBRARIES & "vibrant" & SUFFIX & ".µ")
		Make Project
		Close Project
		
		open (LIBRARIES & "vibgif" & SUFFIX & ".µ")
		Make Project
		Close Project
		
		open (LIBRARIES & "ncbiacc" & SUFFIX & ".µ")
		Make Project
		Close Project
		
		open (LIBRARIES & "ncbidesk" & SUFFIX & ".µ")
		Make Project
		Close Project
		
		open (LIBRARIES & "ncbitool" & SUFFIX & ".µ")
		Make Project
		Close Project
		
		open (LIBRARIES & "ncbimmdb" & SUFFIX & ".µ")
		Make Project
		Close Project
		
		open (LIBRARIES & "ncbicn3d" & SUFFIX & ".µ")
		Make Project
		Close Project
		
	end tell
end DoCompileLibraries

(* ==== This section populates and builds the libraries ==== *)

with timeout of 60000 seconds
	my DoSetGlobals()
	
	my CreateFolders()
	my CopyPrefixes()
	
	tell application "CodeWarrior IDE 4.0.4"
		
		activate
		
		(* ==== This subsection populates all the libraries with the appropriate source files ==== *)
		
		(* ================================================================================ *)
		Create Project {LIBRARIES & "ncsasock" & SUFFIX & ".µ"}
		
		my DoSetPreferences()
		Set Preferences of panel "PPC Project" to {File Name:"ncsasockppc"}
		Set Preferences of panel "68K Project" to {File Name:"ncsasock68k"}
		Set Preferences of panel "PPC CodeGen" to {Struct Alignment:Align_68k}
		Set Preferences of panel "68K CodeGen" to {Four Bytes Ints:true}
		Set Preferences of panel "C/C++ Compiler" to {Prefix File:"net.pfx"}
		Set Preferences of panel "C/C++ Compiler" to {Require Function Prototypes:false}
		Set Preferences of panel "Access Paths" to {User Paths:{{name:NCBISOURCE & "connect:ncsasock:", origin:absolute}}}
		my DoSetPaths2()
		
		repeat with i in {"dprintf", "netdb", "rexec", "socket", "sock_std", "sock_tcp", "sock_udp", "sock_utl", "syslog", "tcpglue", "unixlib"}
			Add Files {NCBISOURCE & "connect:ncsasock:" & i & ".c"} To Segment 1
		end repeat
		
		Add Files {METROWERKSCW & "MacOS Support:Headers:MacTCP Headers:dnr.c"} To Segment 1
		
		Reset File Paths
		Close Project
		
		
		(* ================================================================================ *)
		Create Project {LIBRARIES & "ncsasockOT" & SUFFIX & ".µ"}
		
		my DoSetPreferences()
		Set Preferences of panel "PPC Project" to {File Name:"ncsasockOTppc"}
		Set Preferences of panel "68K Project" to {File Name:"ncsasocOT68k"}
		Set Preferences of panel "PPC CodeGen" to {Struct Alignment:Align_68k}
		Set Preferences of panel "68K CodeGen" to {Four Bytes Ints:true}
		Set Preferences of panel "C/C++ Compiler" to {Prefix File:"net.pfx"}
		Set Preferences of panel "C/C++ Compiler" to {Require Function Prototypes:false}
		Set Preferences of panel "Access Paths" to {User Paths:{{name:NCBISOURCE & "connect:ncsasock:", origin:absolute}}}
		my DoSetPaths2()
		
		repeat with i in {"dprintf", "OTnetdb", "rexec", "socket", "sock_std", "sock_tcp", "sock_udp", "OTsock_utl", "syslog", "OTtcpglue", "unixlib"}
			Add Files {NCBISOURCE & "connect:ncsasock:" & i & ".c"} To Segment 1
		end repeat
		
		Add Files {METROWERKSCW & "MacOS Support:Universal:Libraries:StubLibraries:ThreadsLib"} To Segment 1
		
		repeat with i in {"OpenTransportAppPPC.o", "OpenTptInetPPC.o", "OpenTransportLib", "OpenTptInternetLib"}
			Add Files {OTLIB & i} To Segment 1
		end repeat
		
		Reset File Paths
		Close Project
		
		
		(* ================================================================================ *)
		Create Project {LIBRARIES & "ncbi" & SUFFIX & ".µ"}
		
		my DoSetPreferences()
		Set Preferences of panel "PPC Project" to {File Name:"ncbippc"}
		Set Preferences of panel "68K Project" to {File Name:"ncbi68k"}
		Set Preferences of panel "Access Paths" to {User Paths:{{name:NCBISOURCE & "corelib:", origin:absolute}}}
		Set Preferences of panel "Access Paths" to {User Paths:{{name:NCBISOURCE & "connect:", origin:absolute}}}
		Set Preferences of panel "Access Paths" to {User Paths:{{name:NCBISOURCE & "asnlib:", origin:absolute}}}
		my DoSetPaths2()
		
		repeat with i in {"ncbibs", "ncbienv", "ncbierr", "ncbifile", "ncbimath", "ncbimem", "ncbimisc", "ncbimsg", "ncbiprop", "ncbisgml", Â
			"ncbistr", "ncbitime", "ncbilang", "gifgen", "togif", "ncbithr", "tsprintf", "tree", "wwwutils", "matrix", "binary"}
			Add Files {NCBISOURCE & "corelib:" & i & ".c"} To Segment 1
		end repeat
		
		repeat with i in {"connectn", "connutil", "con_url", "con_sock", "con_stateless", "ncbibuf", "ncbi_buffer", "ncbisock", Â
			"ncbi_socket_", "ncbi_core", "ncbi_util", "urlquery"}
			Add Files {NCBISOURCE & "connect:" & i & ".c"} To Segment 1
		end repeat
		
		repeat with i in {"asnbufo", "asndebin", "asnenbin", "asngen", "asngenob", "asnio", "asnlex", "asnlext", "asnout", "asnprint", "asntypes"}
			Add Files {NCBISOURCE & "asnlib:" & i & ".c"} To Segment 1
		end repeat
		
		Reset File Paths
		Close Project
		
		
		(* ================================================================================ *)
		Create Project {LIBRARIES & "ncbiobj" & SUFFIX & ".µ"}
		
		my DoSetPreferences()
		Set Preferences of panel "PPC Project" to {File Name:"ncbiobjppc"}
		Set Preferences of panel "68K Project" to {File Name:"ncbiobj68k"}
		Set Preferences of panel "Access Paths" to {User Paths:{{name:NCBISOURCE & "object:", origin:absolute}}}
		Set Preferences of panel "Access Paths" to {User Paths:{{name:NCBISOURCE & "access:", origin:absolute}}}
		Set Preferences of panel "Access Paths" to {User Paths:{{name:NCBISOURCE & "api:", origin:absolute}}}
		Set Preferences of panel "Access Paths" to {User Paths:{{name:NCBISOURCE & "ddv:", origin:absolute}}}
		my DoSetPaths2()
		
		repeat with i in {"objacces", "objalign", "objall", "objbibli", "objblock", "objcode", "objfdef", "objfeat", "objgen", "objloc", Â
			"objmdrs", "objmedli", "objmime", "objproj", "objprt", "objpub", "objpubme", "objres", "objseq", "objsset", "objalignloc", "objsub"}
			Add Files {NCBISOURCE & "object:" & i & ".c"} To Segment 1
		end repeat
		
		repeat with i in {"objent2", "ent2api", "pmfapi", "qblastapi", "vecscnapi"}
			Add Files {NCBISOURCE & "access:" & i & ".c"} To Segment 1
		end repeat
		
		repeat with i in {"asn2ff1", "asn2ff2", "asn2ff3", "asn2ff4", "asn2ff5", "asn2ff6", "codon", "edutil", "ffprint", Â
			"findrepl", "ftusrstr", "gather", "gbfeat", "gbftglob", "gbparint", "jzcoll", "jzmisc", "lsqfetch", Â
			"maputil", "mconsist", "objmgr", "prtutil", "satutil", "seqmgr", "seqport", "sequtil", "simple", Â
			"sqnutil1", "sqnutil2", "sqnutil3", "subutil", "tofasta", "tofile", "tomedlin", "txalign", "utilpars", Â
			"utilpub", "salpstat", "salpedit", "valid", "alignmgr", "alignval", "wprint", "salpacc", "salprop", Â
			"salsap", "salstruc", "salutil", "udvseq", "blocks", "pgppop", "samutil", "ddvcolor", "viewmgr"}
			Add Files {NCBISOURCE & "api:" & i & ".c"} To Segment 1
		end repeat
		
		repeat with i in {"ddvcreate"}
			Add Files {NCBISOURCE & "ddv:" & i & ".c"} To Segment 1
		end repeat
		
		Reset File Paths
		Close Project
		
		
		(* ================================================================================ *)
		Create Project {LIBRARIES & "ncbicdr" & SUFFIX & ".µ"}
		
		my DoSetPreferences()
		Set Preferences of panel "PPC Project" to {File Name:"ncbicdrppc"}
		Set Preferences of panel "68K Project" to {File Name:"ncbicdr68k"}
		Set Preferences of panel "Access Paths" to {System Paths:{{name:NCBISOURCE & "cdromlib:", origin:absolute}}}
		my DoSetPaths2()
		
		repeat with i in {"casn", "cdconfig", "cdentrez", "cdnewlib", "cdromlib", "objentr"}
			Add Files {NCBISOURCE & "cdromlib:" & i & ".c"} To Segment 1
		end repeat
		
		Reset File Paths
		Close Project
		
		
		(* ================================================================================ *)
		Create Project {LIBRARIES & "vibrant" & SUFFIX & ".µ"}
		
		my DoSetPreferences()
		Set Preferences of panel "PPC Project" to {File Name:"vibrantppc"}
		Set Preferences of panel "68K Project" to {File Name:"vibrant68k"}
		Set Preferences of panel "Access Paths" to {System Paths:{{name:NCBISOURCE & "vibrant:", origin:absolute}}}
		my DoSetPaths2()
		
		repeat with i in {"ncbidraw", "vibbutns", "vibextra", "vibfiles", "vibforms", "vibgroup", "viblists", "vibmenus", Â
			"vibprmpt", "vibsbars", "vibslate", "vibtexts", "vibutils", "vibwndws", "document", "drawing", "mapping", Â
			"palette", "picture", "table", "viewer", "diagnost", "image", "pictur3d", "prim3d1", "prim3d2", "prim3d3", Â
			"prim3d4", "prim3d5", "viewer3d", "vibmouse", "imagelst", "treeview", "shim3d", "odlbox"}
			Add Files {NCBISOURCE & "vibrant:" & i & ".c"} To Segment 1
		end repeat
		
		Reset File Paths
		Close Project
		
		
		(* ================================================================================ *)
		Create Project {LIBRARIES & "vibgif" & SUFFIX & ".µ"}
		
		my DoSetPreferences()
		Set Preferences of panel "PPC Project" to {File Name:"vibgifppc"}
		Set Preferences of panel "68K Project" to {File Name:"vibgif68k"}
		Set Preferences of panel "C/C++ Compiler" to {Prefix File:"gif.pfx"}
		Set Preferences of panel "Access Paths" to {System Paths:{{name:NCBISOURCE & "gif:", origin:absolute}}}
		Set Preferences of panel "Access Paths" to {System Paths:{{name:NCBISOURCE & "vibrant:", origin:absolute}}}
		my DoSetPaths2()
		
		repeat with i in {"gdfontg", "gdfontl", "gdfontmb", "gdfonts", "gdfontt", "pictogif"}
			Add Files {NCBISOURCE & "gif:" & i & ".c"} To Segment 1
		end repeat
		
		repeat with i in {"ncbidraw", "drawing", "mapping", "picture"}
			Add Files {NCBISOURCE & "vibrant:" & i & ".c"} To Segment 1
		end repeat
		
		Reset File Paths
		Close Project
		
		
		(* ================================================================================ *)
		Create Project {LIBRARIES & "ncbiacc" & SUFFIX & ".µ"}
		
		my DoSetPreferences()
		Set Preferences of panel "PPC Project" to {File Name:"ncbiaccppc"}
		Set Preferences of panel "68K Project" to {File Name:"ncbiacc68k"}
		Set Preferences of panel "Access Paths" to {User Paths:{{name:NCBISOURCE & "cdromlib:", origin:absolute}}}
		my DoSetPaths2()
		
		repeat with i in {"accentr", "accutils", "accmmdbs", "acccn3ds"}
			Add Files {NCBISOURCE & "cdromlib:" & i & ".c"} To Segment 1
		end repeat
		
		Reset File Paths
		Close Project
		
		
		(* ================================================================================ *)
		Create Project {LIBRARIES & "ncbidesk" & SUFFIX & ".µ"}
		
		my DoSetPreferences()
		Set Preferences of panel "PPC Project" to {File Name:"ncbideskppc"}
		Set Preferences of panel "68K Project" to {File Name:"ncbidesk68k"}
		Set Preferences of panel "Access Paths" to {User Paths:{{name:NCBISOURCE & "desktop:", origin:absolute}}}
		Set Preferences of panel "Access Paths" to {User Paths:{{name:NCBISOURCE & "ddv:", origin:absolute}}}
		my DoSetPaths2()
		
		repeat with i in {"apparam", "biosrc", "bspview", "cdrgn", "dlgutil1", "dlgutil2", "dotmatrx", "drawseq", Â
			"fea2seg", "fstyle", "glbpic", "gbfview", "gphview", "gphdraw", "gxydraw", "gtrdraw", "import", Â
			"layout", "mapgene", "medview", "prtgene", "pubdesc", "saldist", "saled", "salfiles", "salpanel", Â
			"salparam", "salsa", "seqsub", "seqfltr", "seqgraph", "seqgrphx", "seqmtrx", "seqpcc", "seqanal", Â
			"aacomp", "seqscrl", "smdlg1", "smdlg2", "smdlg3", "smtools", "styleman", "stylemui", "vsm", Â
			"vsmfile", "vsmutil", "udvgraph", "udvopen", "udvpanel"}
			Add Files {NCBISOURCE & "desktop:" & i & ".c"} To Segment 1
		end repeat
		
		repeat with i in {"ddvclick", "ddvgraph", "ddvopen", "ddvpanel"}
			Add Files {NCBISOURCE & "ddv:" & i & ".c"} To Segment 1
		end repeat
		
		Reset File Paths
		Close Project
		
		
		(* ================================================================================ *)
		Create Project {LIBRARIES & "ncbitool" & SUFFIX & ".µ"}
		
		my DoSetPreferences()
		Set Preferences of panel "PPC Project" to {File Name:"ncbitoolppc"}
		Set Preferences of panel "68K Project" to {File Name:"ncbitool68k"}
		Set Preferences of panel "Access Paths" to {User Paths:{{name:NCBISOURCE & "tools:", origin:absolute}}}
		my DoSetPaths2()
		
		repeat with i in {"csim", "db_slist", "dust", "falign", "g_any", "pobutil", "stsutil", "sec", "sim2", "sim3", Â
			"sim4", "simutil", "slist", "suggadj", "suggest", "suggslp", "tfuns", "toasn3", "toporg", Â
			"blast", "blastutl", "blastool", "splutil", "actutils", "gapxdrop", "bandalg0", "bandalg1", Â
			"bandalg2", "bandalg3", "bandalg4", "bandalg5", "bandalgn", "blastkar", "lookup", "posit", Â
			"readdb", "ncbisam", "ncbisort", "salign", "salptool", "urkutil", "urkpcc", "urkptpf", "urkepi", Â
			"urkfltr", "urkdust", "urksigu", "seg", "urkbias", "urkcnsrt", "urktree", "pattern1", "pseed3", Â
			"impatool", "posit2", "newkar", "mbalign", "mbutils", "mblast", "vecscrn", "rpsutil"}
			Add Files {NCBISOURCE & "tools:" & i & ".c"} To Segment 1
		end repeat
		
		Reset File Paths
		Close Project
		
		
		(* ================================================================================ *)
		Create Project {LIBRARIES & "ncbimmdb" & SUFFIX & ".µ"}
		
		my DoSetPreferences()
		Set Preferences of panel "PPC Project" to {File Name:"ncbimmdbppc"}
		Set Preferences of panel "68K Project" to {File Name:"ncbimmdb68k"}
		Set Preferences of panel "Access Paths" to {User Paths:{{name:NCBISOURCE & "biostruc:", origin:absolute}}}
		my DoSetPaths2()
		
		repeat with i in {"corematx", "dvncode", "objmmdb1", "objmmdb2", "objmmdb3", "mmdbapi", "mmdbapi1", "mmdbapi2", Â
			"mmdbapi3", "mmdbapi4", "mmdbentr", "vastsubs", "prunebsc"}
			Add Files {NCBISOURCE & "biostruc:" & i & ".c"} To Segment 1
		end repeat
		
		Reset File Paths
		Close Project
		
		
		(* ================================================================================ *)
		Create Project {LIBRARIES & "ncbicn3d" & SUFFIX & ".µ"}
		
		my DoSetPreferences()
		Set Preferences of panel "PPC Project" to {File Name:"ncbicn3dppc"}
		Set Preferences of panel "68K Project" to {File Name:"ncbicn3d68k"}
		Set Preferences of panel "Access Paths" to {User Paths:{{name:NCBISOURCE & "cn3d:", origin:absolute}}}
		my DoSetPaths2()
		
		repeat with i in {"algorend", "cn3dwin", "cn3dentr", "cn3dmsel", "cn3dopen", "cn3dsave", "cn3dxprt", "cn3dmsg", Â
			"cn3dmatn", "cn3dmodl", "cn3dshim", "cn3dmesh", "cn3dstyl", "seqcons"}
			Add Files {NCBISOURCE & "cn3d:" & i & ".c"} To Segment 1
		end repeat
		
		Reset File Paths
		Close Project
		
		
		(* ==== This subsection compiles the newly-populated libraries ==== *)
		
		if MAKELIBS = "TRUE" then
			my DoCompileLibraries()
		end if
		
	end tell
end timeout

