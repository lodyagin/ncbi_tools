$!
$!  MAKENET.TWG (OpenVMS VAX, The Wollongong Group TCP/IP suite)
$!
$!   Usage: @makenet.twg
$!
$!   Builds the network interface, Entrez network access, and client TCP
$!   libraries.
$!
$!   Creates the applications:
$!
$!      NetEntCf - A version of the Entrez configuration program, EntrezCf,
$!                  which includes network support
$!      NEntrez  - Network version of Entrez
$!      CEntrez  - Combo version of Entrez which works with CD-ROMs
$!                  and Network services.  This version uses CD-ROM data
$!                  when available, network services otherwise.
$!
$!   William A. Gilbert
$!   Whitehead Institute      gilbert@wi.mit.edu
$!   Univ. of New Hampshire   gilbert@unh.edu
$!--------------------------------------------------------------------------
$ save_verify = f$verify("yes")
$!
$! Definitions
$! Set up include directory definitions. These are specific for The
$! Wollongong Group's TCP/IP suite.
$!
$! Support for TGV, Inc. Multinet is in [-.MAKE]MAKENET.TGV
$! Support for DEC's UCX is underway.
$!
$ define/NoLog sys     twg$tcp:[netdist.include.sys]
$ define/NoLog netinet twg$tcp:[netdist.include.netinet]
$ define/NoLog arpa    twg$tcp:[netdist.include.arpa]
$!
$ CC = "cc/nolist/include=([],[-.include])
$ CCFLAGS = "/define=(NETP_INET_TWG)"
$!
$!--------------------------------------------------------------------------
$! Move Network include files into NCBI include area
$!
$ copy [-.network.entrez.client]*.h  [-.include]     ! Entrez network access
$ copy [-.network.nsclilib]*.h [-.include]           ! Network services 
$ purge [-.include]
$!
$!--------------------------------------------------------------------------
$!
$! Make Network Interface library, netcli
$!
$!    N.B. Due to some problem between the compiler and the librarian
$!         these object files will not produce a runnable executable
$!         if linked in as a library, hence seperate files should be
$!         used during linking.
$!
$ CC 'CCFLAGS' [-.network.nsclilib]ni_lib.c
$ CC 'CCFLAGS' [-.network.nsclilib]ni_msg.c
$ CC [-.network.nsclilib]ni_error.c
$ CC [-.network.nsclilib]ni_encrs.c
$!
$! Don't make library for now, just copy object file to library directory
$!
$ rename ni_lib.obj,ni_msg.obj,ni_error.obj,ni_encrs.obj [-.lib]
$!! Librarian/create/object [-.lib]netcli.olb  ni_lib,ni_msg,ni_error,ni_encrs
$!! delete ni_lib.obj;*,ni_msg.obj;*,ni_error.obj;*,ni_encrs.obj;*
$!--------------------------------------------------------------------------
$!
$! Make Network Dispatcher/Entrez access library, netentr
$!
$ CC [-.network.entrez.client]netentr.c
$ CC [-.network.entrez.client]netlib.c
$ CC [-.network.entrez.client]objneten.c
$!
$  Librarian/create/Object [-.lib]netentr.olb  netentr.obj,netlib.obj,objneten.obj
$!
$ delete netentr.obj;*,netlib.obj;*,objneten.obj;*
$!
$! Make library which depends upon both Vibrant and the network client
$!
$ CC [-.network.vibnet]netcnfg.c
$ CC [-.network.vibnet]trmlst.c
$ CC [-.network.vibnet]docsum.c
$!
$  Librarian/create/Object [-.lib]vibnet.olb  netcnfg.obj,trmlst.obj,docsum.obj
$!
$ delete netcnfg.obj;*,trmlst.obj;*,docsum.obj;*
$!
$!=====================  A P P L I C A T I O N S  ==========================
$!
$! Make version of EntrezCf which has network support
$!
$ CCFLAGS = "/define=(NETP_INET_TWG,WIN_MOTIF)"
$ CC 'CCFLAGS' [-.demo]netentcf.c/object=netentcf.obj 
$!
$!  Link EntrezCf
$!
$ link/exe=netentcf.exe sys$input/option
[]netentcf.obj
[-.lib]libvibrant.olb/library
[-.lib]libncbicdr.olb/library
[-.lib]vibnet.olb/library
[-.lib]netentr.olb/library
[-.lib]ni_lib.obj
[-.lib]ni_msg.obj
[-.lib]ni_error.obj
[-.lib]ni_encrs.obj
[-.lib]libncbi.olb/library
twg$common:[netdist.etc]twg_rtl.exe/shareable
sys$share:decw$xlibshr.exe/shareable
sys$share:decw$dxmlibshr.exe/shareable
sys$share:decw$xmlibshr.exe/shareable
sys$share:decw$xtshr.exe/shareable
sys$share:vaxcrtl.exe/shareable
$!-------------------------------------------------------------------------
$!
$ deassign sys
$ deassign netinet
$ deassign arpa
$ if .not.save_verify then set noverify
$ exit

